MODULE UTIL
  INTERFACE SIMP
    MODULE PROCEDURE SIMP1, SIMPC
  END INTERFACE SIMP

  INTERFACE DIFF
    MODULE PROCEDURE DIFF1, DIFFC, DIFF_PERIODIC, DIFFC_PERIODIC
  END INTERFACE DIFF

  INTERFACE INTEGRATE
    MODULE PROCEDURE INTEGRATE2, INTEGRATE3
  END INTERFACE INTEGRATE

  INTERFACE NORM
    MODULE PROCEDURE NORM_1I, NORM_1R, NORM_2I, NORM_2R, NORM_3I, NORM_3R
  END INTERFACE NORM

  INTERFACE RAD
    MODULE PROCEDURE RAD_1I, RAD_1R, RAD_2I, RAD_2R, RAD_3I, RAD_3R, RAD_2R_2C , RAD_1R_2C
  END INTERFACE RAD

CONTAINS

PURE FUNCTION SIMP1(F, DX) RESULT (VALUE)
! Does the spatial integration with Simpson's rule.
! N refer to the number of integration points, DX space step, and
! F is the function to be integrated.
  IMPLICIT NONE
  REAL (8), DIMENSION(0:), INTENT(IN) :: F
  REAL (8), INTENT(IN) :: DX
  REAL (8) :: VALUE

  REAL (8) :: F1, F2
  INTEGER :: I, N

  N = SIZE(F) - 1
  
  F1 = F(1) + F(N-1) 
  F2 = F(2) 
  DO I = 3, N-3, 2
     F1 = F1 + F(I)
     F2 = F2 + F(I+1)
  END DO
  VALUE = DX*(F(0) + 4.0D0*F1 + 2.0D0*F2 + F(N))/3.0D0
END FUNCTION SIMP1

PURE FUNCTION SIMPC(F, DX) RESULT (VALUE)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:), INTENT(IN) :: F
  REAL (8), INTENT(IN) :: DX
  COMPLEX (8) :: VALUE
  COMPLEX (8) :: F1, F2
  INTEGER :: I, N
  N = SIZE(F) - 1
  F1 = F(1) + F(N-1)  
  F2 = F(2)
  DO I = 3, N-3, 2
     F1 = F1 + F(I)
     F2 = F2 + F(I+1)
  END DO
  VALUE = DX*(F(0) + 4.0D0*F1 + 2.0D0*F2 + F(N))/3.0D0
END FUNCTION SIMPC

PURE FUNCTION DIFF1(P,DX) RESULT (DP)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:), INTENT(IN) :: P
  REAL (8), INTENT(IN) :: DX
  REAL (8), DIMENSION(0:SIZE(P)-1) :: DP
  INTEGER :: I, N
  N = SIZE(P) - 1
  DP(0) = 0.0D0
  DP(1) = (P(2) - P(0))/(2.0D0*DX)
  DO I=2, N-2
    DP(I) = (P(I-2)-8.0D0*P(I-1)+8.0D0*P(I+1)-P(I+2))/(12.0D0*DX)
  END DO
  DP(N-1) = (P(N) - P(N-2))/(2.0D0*DX)
  DP(N) = 0.0D0
END FUNCTION DIFF1
!
PURE FUNCTION DIFFC(P,DX) RESULT (DP)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:), INTENT(IN) :: P
  REAL (8), INTENT(IN) :: DX
  COMPLEX (8), DIMENSION(0:SIZE(P)-1) :: DP
  INTEGER :: I, N
  N = SIZE(P) - 1
  DP(0) = 0.0D0
  DP(1) = (P(2) - P(0))/(2.0D0*DX)
  DO I=2, N-2
    DP(I) = (P(I-2)-8.0D0*P(I-1)+8.0D0*P(I+1)-P(I+2))/(12.0D0*DX)
  END DO
  DP(N-1) = (P(N) - P(N-2))/(2.0D0*DX)
  DP(N) = 0.0D0
END FUNCTION DIFFC

PURE FUNCTION DIFFC_PERIODIC(P,DX,IP) RESULT (DP)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:), INTENT(IN) :: P
  REAL (8), INTENT(IN) :: DX
  INTEGER, INTENT(IN) :: IP
  COMPLEX (8), DIMENSION(0:SIZE(P)-1) :: DP
  COMPLEX (8), DIMENSION(-IP:SIZE(P)-1+IP) :: CP
  INTEGER :: I, N
  N = SIZE(P) - 1
  CP(-2) = P(N-2)
  CP(-1) = P(N-1)
  CP(0:N-1) = P
  CP(N) = P(0)
  CP(N+1) = P(1)
  FORALL(I=0:N-1)
    DP(I) = (CP(I-2)-8.0D0*CP(I-1)+8.0D0*CP(I+1)-CP(I+2))/(12.0D0*DX)
  END FORALL
END FUNCTION DIFFC_PERIODIC

PURE FUNCTION DIFF_PERIODIC(P,DX,IP) RESULT (DP)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:), INTENT(IN) :: P
  REAL (8), INTENT(IN) :: DX
  INTEGER, INTENT(IN) :: IP
  REAL (8), DIMENSION(0:SIZE(P)-1) :: DP
  REAL (8), DIMENSION(-IP:SIZE(P)-1+IP) :: CP
  INTEGER :: I, N
  N = SIZE(P) - 1
  CP(-2) = P(N-2)
  CP(-1) = P(N-1)
  CP(0:N-1) = P
  CP(N) = P(0)
  CP(N+1) = P(1)
  FORALL(I=0:N-1)
    DP(I) = (CP(I-2)-8.0D0*CP(I-1)+8.0D0*CP(I+1)-CP(I+2))/(12.0D0*DX)
  END FORALL
END FUNCTION DIFF_PERIODIC

PURE FUNCTION INTEGRATE2(U, DX, DY) RESULT(RES)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:, 0:), INTENT(IN) :: U
  REAL (8), INTENT (IN) :: DX, DY
  REAL (8) :: RES
  REAL (8), DIMENSION(0:SIZE(U,1)-1) :: TMP1D
  INTEGER :: I
  FORALL (I = 0:SIZE(U,1)-1) TMP1D(I) = SIMP(U(I,:), DY)
  RES = SIMP(TMP1D, DX)
END FUNCTION INTEGRATE2

PURE FUNCTION INTEGRATE3(U, DX, DY, DZ) RESULT(RES)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:, 0:, 0:), INTENT(IN) :: U
  REAL (8), INTENT (IN) :: DX, DY, DZ
  REAL (8) :: RES
  REAL (8), DIMENSION(0:SIZE(U,1)-1, 0:SIZE(U,2)-1) :: TMP2D
  REAL (8), DIMENSION(0:SIZE(U,1)-1) :: TMP1D
  INTEGER :: I, J
  FORALL (I = 0:SIZE(U,1)-1)
     FORALL (J = 0:SIZE(U,2)-1) TMP2D(I,J) = SIMP(U(I,J,:), DZ)
     TMP1D(I) = SIMP(TMP2D(I,:), DY)
  END FORALL
  RES = SIMP(TMP1D, DX)
END FUNCTION INTEGRATE3

PURE SUBROUTINE NORM_1I(P, DX, ZNORM)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:), INTENT(INOUT) :: P
  REAL (8), INTENT(IN) :: DX
  REAL (8), INTENT(OUT) :: ZNORM
!
  INTEGER :: I, NX
  REAL (8), DIMENSION(0:SIZE(P)-1) :: P2

  NX = SIZE(P) - 1
!
  DO I = 0, NX
      P2(I) = P(I) * P(I)
  END DO
  ZNORM = SIMP(P2, DX)
END SUBROUTINE NORM_1I

PURE SUBROUTINE NORM_1R(CP, DX, ZNORM)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:), INTENT(IN) :: CP
  REAL (8), INTENT(IN) :: DX
  REAL (8), INTENT(OUT) :: ZNORM
!
  INTEGER :: I, NX
  REAL (8), DIMENSION(0:SIZE(CP)-1) :: P2

  NX = SIZE(CP) - 1
!
  DO I = 0, NX
    P2(I) = REAL(CP(I)) * REAL(CP(I)) + AIMAG(CP(I)) * AIMAG(CP(I))
  END DO
  ZNORM = SIMP(P2, DX)
END SUBROUTINE NORM_1R

PURE SUBROUTINE NORM_2I(P, DX, DY, ZNORM)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:,0:), INTENT(INOUT) :: P
  REAL (8), INTENT(IN) :: DX, DY
  REAL (8), INTENT(OUT) :: ZNORM

  INTEGER :: I, J, NX, NY
  REAL (8), DIMENSION(0:SIZE(P,1)-1, 0:SIZE(P,2)-1) :: P2
  !REAL (8) :: SQ_NORM

  NX = SIZE(P,1) - 1
  NY = SIZE(P,2) - 1

  DO I = 0, NX
    DO J = 0, NY
      P2(I,J) = P(I,J) * P(I,J)
    END DO
  END DO
!
  ZNORM = INTEGRATE(P2, DX, DY)
!
!  DO I = 0, NX
!    DO J = 0, NY
!      P(I,J) = P(I,J) / SQ_NORM
!    END DO
!  END DO
END SUBROUTINE NORM_2I

PURE SUBROUTINE NORM_2R(CP, DX, DY, ZNORM)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:,0:), INTENT(IN) :: CP
  REAL (8), INTENT(IN) :: DX, DY
  REAL (8), INTENT(OUT) :: ZNORM

  INTEGER :: I, J, NX, NY
  REAL (8), DIMENSION(0:SIZE(CP,1)-1, 0:SIZE(CP,2)-1) :: P2
  REAL (8) :: TMP!, SQ_NORM

  NX = SIZE(CP,1) - 1
  NY = SIZE(CP,2) - 1

  DO I = 0, NX
    DO J = 0, NY
      TMP = ABS(CP(I,J))
      P2(I,J) = TMP * TMP
    END DO
  END DO
 !
  ZNORM = INTEGRATE(P2, DX, DY)
  !SQ_NORM = SQRT(ZNORM)
!
  !DO I = 0, NX
  !  DO J = 0, NY
  !    CP(I,J) = CP(I,J) / SQ_NORM
  !  END DO
  !END DO
END SUBROUTINE NORM_2R

PURE SUBROUTINE NORM_3I(P, DX, DY, DZ, ZNORM)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:,0:,0:), INTENT(INOUT) :: P
  REAL (8), INTENT(IN) :: DX, DY, DZ
  REAL (8), INTENT(OUT) :: ZNORM

  INTEGER :: I, J, K, NX, NY, NZ
  REAL (8), DIMENSION(0:SIZE(P,1)-1, 0:SIZE(P,2)-1, 0:SIZE(P,3)-1) :: P2
  REAL (8) :: SQ_NORM

  NX = SIZE(P,1) - 1
  NY = SIZE(P,2) - 1
  NZ = SIZE(P,3) - 1

  DO I = 0, NX; DO J = 0, NY;  DO K = 0, NZ
      P2(I,J,K) = P(I,J,K) * P(I,J,K)
  END DO; END DO; END DO

  ZNORM = INTEGRATE(P2, DX, DY, DZ)
  SQ_NORM = SQRT(ZNORM)

  !DO I = 0, NX; DO J = 0, NY;  DO K = 0, NZ
  !   P(I,J,K) = P(I,J,K)/SQ_NORM
  !END DO; END DO; END DO
END SUBROUTINE NORM_3I

PURE SUBROUTINE NORM_3R(CP, DX, DY, DZ, ZNORM)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:,0:,0:), INTENT(INOUT) :: CP
  REAL (8), INTENT(IN) :: DX, DY, DZ
  REAL (8), INTENT(OUT) :: ZNORM

  INTEGER :: I, J, K, NX, NY, NZ
  REAL (8), DIMENSION(0:SIZE(CP,1)-1, 0:SIZE(CP,2)-1, 0:SIZE(CP,3)-1) :: P2
  REAL (8) :: TMP!, SQ_NORM

  NX = SIZE(CP,1) - 1
  NY = SIZE(CP,2) - 1
  NZ = SIZE(CP,3) - 1

  DO I = 0, NX; DO J = 0, NY;  DO K = 0, NZ
     TMP = ABS(CP(I,J,K))
     P2(I,J,K) = TMP * TMP
  END DO; END DO; END DO
 
  ZNORM = INTEGRATE(P2, DX, DY, DZ)
 
  !SQ_NORM = SQRT(ZNORM)
  !DO I = 0, NX; DO J = 0, NY;  DO K = 0, NZ
  !    CP(I,J,K) = CP(I,J,K)/SQ_NORM
  !END DO; END DO; END DO
END SUBROUTINE NORM_3R

PURE SUBROUTINE RAD_1I(P, X2, DX, RMS)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:), INTENT(IN) :: P
  REAL (8), DIMENSION(0:), INTENT(IN) :: X2
  REAL (8), INTENT(IN) :: DX
  REAL (8), INTENT(OUT) :: RMS
  REAL (8), DIMENSION(0:SIZE(P)-1) :: P2
  P2 = X2*P*P
  RMS = SQRT(SIMP(P2, DX))
END SUBROUTINE RAD_1I

PURE SUBROUTINE RAD_1R_2C(CP, X2, DX, RMS)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(:,0:), INTENT(IN) :: CP
  REAL (8), DIMENSION(0:), INTENT(IN) :: X2
  REAL (8), INTENT(IN) :: DX
  REAL (8), INTENT(OUT) :: RMS
  REAL (8), DIMENSION(0:SIZE(CP,2)-1) :: P
  INTEGER :: I
  DO I = 0, SIZE(X2) - 1
     P(I) = SQRT(SUM(REAL(CP(:,I))**2 + AIMAG(CP(:,I))**2))
  END DO
  CALL RAD(P, X2, DX, RMS)
END SUBROUTINE RAD_1R_2C

PURE SUBROUTINE RAD_1R(CP, X2, DX, RMS)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:), INTENT(IN) :: CP
  REAL (8), DIMENSION(0:), INTENT(IN) :: X2
  REAL (8), INTENT(IN) :: DX
  REAL (8), INTENT(OUT) :: RMS
  REAL (8), DIMENSION(0:SIZE(CP)-1) :: P
  P  = ABS(CP)
  CALL RAD(P, X2, DX, RMS)
END SUBROUTINE RAD_1R

PURE SUBROUTINE RAD_2I(P, R2, X2, Y2, DX, DY, RMS)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:,0:), INTENT(IN) :: P, R2
  REAL (8), DIMENSION(0:), INTENT(IN) :: X2, Y2
  REAL (8), INTENT(IN) :: DX, DY
  REAL (8), DIMENSION(:), INTENT(OUT) :: RMS
!
  INTEGER :: I, J, NX, NY
  REAL (8), DIMENSION(0:SIZE(P,1)-1, 0:SIZE(P,2)-1) :: P2, TMP2D
!
  NX = SIZE(P,1)-1
  NY = SIZE(P,2)-1
!
  P2 = P * P
  FORALL(J=0:NY) TMP2D(:,J) = X2*P2(:,J)
  RMS(1) = SQRT(INTEGRATE(TMP2D, DX, DY))
!
  FORALL(I=0:NX) TMP2D(I,:) = Y2*P2(I,:)
  RMS(2) = SQRT(INTEGRATE(TMP2D, DX, DY))
!
  TMP2D = R2*P2
  RMS(3) = SQRT(INTEGRATE(TMP2D, DX, DY))
END SUBROUTINE RAD_2I

PURE SUBROUTINE RAD_2R(CP, R2, X2, Y2, DX, DY, RMS)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:,0:), INTENT(IN) :: CP
  REAL (8), DIMENSION(0:,0:), INTENT(IN) :: R2
  REAL (8), DIMENSION(0:), INTENT(IN) :: X2, Y2
  REAL (8), INTENT(IN) :: DX, DY
  REAL (8), DIMENSION(:), INTENT(OUT) :: RMS
!
  REAL (8), DIMENSION(0:SIZE(CP,1)-1, 0:SIZE(CP,2)-1) :: P
!
  P = ABS(CP)
  CALL RAD(P, R2, X2, Y2, DX, DY, RMS)
END SUBROUTINE RAD_2R

PURE SUBROUTINE RAD_2R_2C(CP1, CP2, R2, X2, Y2, DX, DY, RMS)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:,0:), INTENT(IN) :: CP1, CP2
  REAL (8), DIMENSION(0:,0:), INTENT(IN) :: R2
  REAL (8), DIMENSION(0:), INTENT(IN) :: X2, Y2
  REAL (8), INTENT(IN) :: DX, DY
  REAL (8), DIMENSION(:), INTENT(OUT) :: RMS
!
  REAL (8), DIMENSION(0:SIZE(CP1,1)-1, 0:SIZE(CP1,2)-1) :: P
!
  P = SQRT(REAL(CP1 * CONJG(CP1) + CP2 * CONJG(CP2)))
  CALL RAD(P, R2, X2, Y2, DX, DY, RMS)
END SUBROUTINE RAD_2R_2C

PURE SUBROUTINE RAD_3I(P, R2, X2, Y2, Z2, DX, DY, DZ, RMS)
  IMPLICIT NONE
  REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: P, R2
  REAL (8), DIMENSION(0:), INTENT(IN) :: X2, Y2, Z2
  REAL (8), INTENT(IN) :: DX, DY, DZ
  REAL (8), DIMENSION(:), INTENT(OUT) :: RMS
!
  INTEGER :: I, J, K, NX, NY, NZ
  REAL (8), DIMENSION(0:SIZE(P,1)-1, 0:SIZE(P,2)-1, 0:SIZE(P,3)-1) :: P2, TMP3D
!
  NX = SIZE(P2,1) - 1
  NY = SIZE(P2,2) - 1
  NZ = SIZE(P2,3) - 1
!
  P2 = P * P
!
  FORALL(J=0:NY, K=0:NZ) TMP3D(:,J,K) = X2*P2(:,J,K)
  RMS(1) = SQRT(INTEGRATE(TMP3D, DX, DY, DZ))
  
  FORALL(I=0:NX, K=0:NZ) TMP3D(I,:,K) = Y2*P2(I,:,K)
  RMS(2) = SQRT(INTEGRATE(TMP3D, DX, DY, DZ))

  FORALL(I=0:NX, J=0:NY) TMP3D(I,J,:) = Z2*P2(I,J,:)
  RMS(3) = SQRT(INTEGRATE(TMP3D, DX, DY, DZ))

  TMP3D = R2 * P2
  RMS(4) = SQRT(INTEGRATE(TMP3D, DX, DY, DZ))
END SUBROUTINE RAD_3I

PURE SUBROUTINE RAD_3R(CP, R2, X2, Y2, Z2, DX, DY, DZ, RMS)
  IMPLICIT NONE
  COMPLEX (8), DIMENSION(0:,0:,0:), INTENT(IN) :: CP
  REAL (8), DIMENSION(0:,0:,0:), INTENT(IN) :: R2
  REAL (8), DIMENSION(0:), INTENT(IN) :: X2, Y2, Z2
  REAL (8), INTENT(IN) :: DX, DY, DZ
  REAL (8), DIMENSION(:), INTENT(OUT) :: RMS
  REAL (8), DIMENSION(0:SIZE(CP,1)-1, 0:SIZE(CP,2)-1, 0:SIZE(CP,3)-1) :: P
  INTEGER :: I, J, K, NX, NY, NZ

  NX = SIZE(P,1) - 1
  NY = SIZE(P,2) - 1
  NZ = SIZE(P,3) - 1

  DO I = 0, NX; DO J = 0, NY;  DO K = 0, NZ
     P(I,J,K) = ABS(CP(I,J,K))
  END DO; END DO; END DO
  CALL RAD(P, R2, X2, Y2, Z2, DX, DY, DZ, RMS)
END SUBROUTINE RAD_3R

END MODULE UTIL

